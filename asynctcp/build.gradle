plugins {
    id 'java-library'
    id 'java-library-distribution'
    id 'maven-publish'
    id 'java-test-fixtures'
}

java {
    modularity.inferModulePath = true
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}


dependencies {
    implementation project(':transport')
    implementation project(':sbe')
    implementation project(':serialization')

    testFixturesImplementation project(':transport')
    testFixturesImplementation project(':serialization')
    testFixturesImplementation 'org.agrona:agrona:1.6.0'

    testImplementation 'org.assertj:assertj-core:3.16.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.2'
    testImplementation testFixtures(project(':transport'))
}

jar {
    manifest {
        attributes('Automatic-Module-Name': 'dev.squaremile.asynctcp')
    }
}

distributions {
    main {
        distributionBaseName = 'asynctcp'
    }
}

repositories {
    mavenLocal()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "$buildDir/repos/releases"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
            url = version.contains('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
        if (System.getenv("GITHUB_ACTOR") != null)
        {
            maven {
                println "Configuring GitHub packages for ${System.getenv("GITHUB_ACTOR")} and version ${version}"
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/squaremiledev/asynctransport"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}